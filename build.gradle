buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'ca.cutterslade.gradle:gradle-dependency-analyze:2.0.0'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

apply plugin: 'ca.cutterslade.analyze'

group 'org.LostCityMapEditor'
version '3.5'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'org.lostcitymapeditor'
    mainClass = 'org.lostcitymapeditor.LostCityMapEditor'
    applicationDefaultJvmArgs = [
            '--add-opens', 'javafx.graphics/javafx.scene.effect=com.gluonhq.attach.util',
            '--add-exports', 'javafx.graphics/javafx.scene.effect=ALL-UNNAMED'
    ]
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.swing']
}

project.ext.lwjglVersion = "3.3.6"
project.ext.lwjglNativesWindows = "natives-windows"
project.ext.lwjglNativesLinux = "natives-linux"
project.ext.lwjglNativesMac = "natives-macos"

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.joml:joml:1.10.5")
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNativesWindows"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNativesWindows"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNativesWindows"

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNativesLinux"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNativesLinux"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNativesLinux"

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNativesMac"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNativesMac"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNativesMac"
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'org.lostcitymapeditor.LostCityMapEditor'
    }

    // Include all dependencies in the JAR
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    // Handle duplicate files
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'LostCityMapEditor'
        jvmArgs = ['-Djava.library.path=$APPDIR/lib']
    }

    addExtraDependencies("javafx")
    addExtraDependencies("lwjgl")

    forceMerge("javafx")
    forceMerge("lwjgl")
}

jlinkZip {
    group = 'distribution'
}

task createDistribution(type: Zip, dependsOn: [build, jlinkZip]) {
    archiveFileName = "LostCityMapEditor-${version}-distribution.zip"
    destinationDirectory = file("${buildDir}/distributions")

    from jar
    from "${buildDir}/image"
}